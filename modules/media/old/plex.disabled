variable plex_name { default = "plex" }
variable media_vol_count { default = 3 }
variable media_vol_size { default = "20Gi" }
variable media_vol_storage_class { default = "longhorn-no-rep-lo-local" }

resource "kubernetes_persistent_volume_claim" "media_volumes" {
  count                = var.media_vol_count
  metadata {
    name = "media-volume-${count.index+1}"
    namespace = var.namespace
  }

  spec {
    access_modes = ["ReadWriteOnce"]
    resources {
      requests = {
        storage = var.media_vol_size
      }
    }
    storage_class_name = var.media_vol_storage_class
  }
}


locals {
  plex_domain = "${var.plex_name}.linuxguru.net"

  media_mounts = [
    for i in range(1, var.media_vol_count + 1) : {
      "extraVolumes[${i}].name" = "media-storage-${i}"
      "extraVolumes[${i}].persistentVolumeClaim.claimName" = "media-volume-${i}"
      "extraVolumeMounts[${i}].name" = "media-storage-${i}"
      "extraVolumeMounts[${i}].mountPath" = "/media/media-storage-${i}"
    }


  ]


  plex_helm_values = {
    "extraEnv.PLEX_UID" = "0",
    "extraEnv.PLEX_GID" = "0",
    "image.tag" = "latest"
    "pullPolicy" = "always"
    "ingress.enabled"   =  true,
    "ingress.ingressClassName" = "public",
    "service.type" = "LoadBalancer"
    "ingress.url" = local.plex_domain,
    "ingress.hosts.0.paths.0.path"     = "/"
    "ingress.hosts.0.paths.0.pathType" = "ImplementationSpecific",
    "ingress.tls.0.hosts.0"    = local.plex_domain,
    "ingress.tls.0.secretName" = "cert-${local.plex_domain}"
    #"ingress.annotations.cert-manager\\.io/cluster-issuer"  = var.cert_issuer,
    "ingress.annotations.cert-manager\\.io/cluster-issuer"  = "letsencrypt",
    "ingress.annotations.external-dns\\.alpha\\.kubernetes\\.io/hostname"  = local.plex_domain,
    "extraVolumes[0].name" = "s3-media-storage",
    "extraVolumes[0].persistentVolumeClaim.claimName" = "movies",
    "extraVolumeMounts[0].name" = "s3-media-storage",
    "extraVolumeMounts[0].mountPath" = "/mnt/s3media"

  }
}



resource helm_release plex {
  name  = "plex"
  repository = "https://raw.githubusercontent.com/plexinc/pms-docker/gh-pages"
  chart = "plex-media-server"
  namespace = var.namespace
  depends_on = [ kubernetes_namespace.namespace ]
  set = concat(
    [for key, value in local.plex_helm_values : { name = key, value = value }],
    flatten([
      for mount in local.media_mounts : [
        for k, v in mount : { name = k, value = v }
      ]
    ])
  )

}


